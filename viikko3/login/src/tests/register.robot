*** Settings ***
Resource  resource.robot
Suite Setup     Open And Configure Browser
Suite Teardown  Close Browser
Test Setup      Reset Application Create User And Go To Register Page

*** Test Cases ***

Register With Valid Username And Password
    Set Username  testuser1
    Set Password  testuser1
    Set Password Confirmation  testuser1
    Click Button  Register
    Register Should Succeed

Register With Too Short Username And Valid Password
    Set Username  te
    Set Password  testuser2
    Set Password Confirmation  testuser2
    Click Button  Register
    Register Should Fail With Message  Username must be at least 3 characters long

Register With Valid Username And Too Short Password
    Set Username  testuser2
    Set Password  test
    Set Password Confirmation  test
    Click Button  Register
    Register Should Fail With Message  Password must be at least 8 characters long

Register With Valid Username And Invalid Password
    Set Username  testuser2
    Set Password  testuser
    Set Password Confirmation  testuser
    Click Button  Register
    Register Should Fail With Message  Password must contain at least 1 non-letter character

Register With Nonmatching Password And Password Confirmation
    Set Username  testuser2
    Set Password  testuser2
    Set Password Confirmation  testuser3
    Click Button  Register
    Register Should Fail With Message  Given passwords did not match

Register With Username That Is Already In Use
    Set Username  kalle
    Set Password  kalle123
    Set Password Confirmation  kalle123
    Click Button  Register
    Register Should Fail With Message  Username already taken

*** Keywords ***
Register Should Succeed
    Welcome Page Should Be Open

Register Should Fail With Message
    [Arguments]  ${message}
    Register Page Should Be Open
    Page Should Contain  ${message}

Set Username
    [Arguments]  ${username}
    Input Text  username  ${username}

Set Password
    [Arguments]  ${password}
    Input Password  password  ${password}

Set Password Confirmation
    [Arguments]  ${password_confirmation}
    Input Password  password_confirmation  ${password_confirmation}

*** Keywords ***
Reset Application Create User And Go To Register Page
    Reset Application
    Create User  kalle  kalle123
    Go To Register Page